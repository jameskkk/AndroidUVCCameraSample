/*
 *  UVCCamera
 *  library and sample to access to UVC web camera on non-rooted Android device
 *
 * Copyright (c) 2014-2018 saki t_saki@serenegiant.com
 *
 *  Licensed under the Apache License, Version 2.0 (the "License");
 *  you may not use this file except in compliance with the License.
 *   You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 *   Unless required by applicable law or agreed to in writing, software
 *   distributed under the License is distributed on an "AS IS" BASIS,
 *   WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 *   See the License for the specific language governing permissions and
 *   limitations under the License.
 *
 *  All files in the folder are under this Apache License, Version 2.0.
 *  Files in the libjpeg-turbo, libusb, libuvc, rapidjson and OpenCV folder
 *  may have a different license, see the respective files.
 */

apply plugin: 'com.android.library'
import org.apache.tools.ant.taskdefs.condition.Os

android {
	compileSdkVersion versionCompiler
	buildToolsVersion versionBuildTool

	defaultConfig {
		minSdkVersion 18
		targetSdkVersion versionTarget
		versionCode 1
		versionName "1.0"
		testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
	}

	buildTypes {
		release {
			minifyEnabled false
			proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
		}
	}

	sourceSets {
		main {
			jniLibs.srcDir 'src/main/libs'
			jni.srcDirs = []
		}
	}
	ndkVersion '21.4.7075529'
}

tasks.withType(JavaCompile) {
	compileTask -> compileTask.dependsOn ndkBuild
}

String getNdkBuildPath() {
//	Properties properties = new Properties()
//	properties.load(project.rootProject.file('local.properties').newDataInputStream())
//	def ndkBuildingDir = properties.getProperty("ndk.dir")
	def ndkBuildingDir = android.ndkDirectory
	def ndkBuildingPath = ndkBuildingDir
	if (Os.isFamily(Os.FAMILY_WINDOWS)) {
		ndkBuildingPath = "$ndkBuildingDir\\ndk-build.cmd"
	} else {
		ndkBuildingPath = "$ndkBuildingDir/ndk-build"
	}
	return ndkBuildingPath
}

task ndkBuild(type: Exec, description: 'Compile JNI source via NDK') {
	println('executing ndkBuild')
	def ndkBuildPath = getNdkBuildPath();
//	commandLine ndkBuildPath '-j4', '-C', file('src/main').absolutePath
	commandLine ndkBuildPath, '-j8', '-C', file('src/main').absolutePath, 'NDK_LIBS_OUT=jniLibs', 'NDK_OUT=obj'
}

task ndkClean(type: Exec, description: 'clean JNI libraries') {
	println('executing ndkBuild clean')
	commandLine getNdkBuildPath(), 'clean', '-C', file('src/main').absolutePath
}

clean.dependsOn 'ndkClean'

dependencies {
	api fileTree(dir: 'libs', include: ['*.jar'])
	androidTestImplementation('com.android.support.test.espresso:espresso-core:3.0.2', {
		exclude group: 'com.android.support', module: 'support-annotations'
	})
	implementation 'com.android.support.constraint:constraint-layout:2.0.4'
	testImplementation 'junit:junit:4.13.2'

	implementation "com.android.support:support-annotations:${supportLibVersion}"
	implementation("com.serenegiant:common:${commonLibVersion}") {
		exclude module: 'support-v4'
	}
}
